#+TITLE: Emacs Init
#+AUTHOR: William Denton
#+EMAIL: wtd@pobox.com

#+property: header-args:emacs-lisp :tangle yes :cache yes :results silent
# #+property: header-args:shell :tangle "setup.sh"
#+property: header-args :tangle no :results silent

#+options: num:nil toc:nil ^:nil
#+startup: fold

# Need to fancy up the LaTeX export so I can read it all like an article.

#+latex_class_options: [10pt]

#+latex_header: \usepackage[T1]{fontenc}

#+latex_header: \usepackage[english]{babel} % English language/hyphenation
#+latex_header: \usepackage[osf]{Baskervaldx}

#+latex_header: \usepackage[activate={true,nocompatibility},final,tracking=true,kerning=true,spacing=true,factor=1100,stretch=10,shrink=10]{microtype}
#+latex_header: \microtypecontext{spacing=nonfrench}

#+latex_header: \usepackage{ccicons}

#+latex_header: \usepackage[margin=2cm]{geometry}

#+latex_header: \usepackage{enumitem}
#+latex_header: \setlist{noitemsep}

#+latex_header: \hypersetup{colorlinks=true,urlcolor=blue,linkcolor=blue,pdfborder={0 0 0}}

* Commentary

In March 2021 I moved my Emacs config into an Org file, but did it in bulk, so many small comments are yet to be done.  I'll expand all this as I refine it.

** Using Org for Emacs management

Diego Zamboni's [[https://leanpub.com/lit-config/read][Literate Configuration]] is relevant and I should read it.

Org inits I looked at:

+ [[https://github.com/sachac/.emacs.d][Sacha Chua]]
+ [[https://github.com/theophilusx/emacs-init-org/][Tim Cross]]
+ [[https://github.com/dieggsy/dotfiles/tree/master/emacs/.emacs.d][Diego]]
+ [[https://github.com/thblt/.emacs.d][Thibault Polge]]
+ [[https://github.com/novoid/dot-emacs][Karl Voit]]
+ [[https://gitlab.com/marcowahl/mw.emacs.d][Marco Wahl]]
+ [[https://github.com/zzamboni/dot-emacs/][Diego Zamboni]]
+ [[https://github.com/tecosaur/emacs-config][tecosaur]]

** Things to try one day:

+ [[https://github.com/politza/pdf-tools][pdf-tools]]

* Warning!

#+begin_src emacs-lisp
;; DO NOT EDIT THIS FILE DIRECTLY
;; It is generated from an Org file.
;;
;; You should make any changes there and regenerate it with C-c C-v t
#+end_src

* Early init

The [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Early-Init-File.html][early init file]] "is loaded before the package system and GUI is initialized, so in it you can customize variables that affect frame appearance as well as the package initialization process."

#+begin_src emacs-lisp :tangle early-init.el
;; DO NOT EDIT THIS FILE DIRECTLY
;; It is generated from an Org file.
;;
;; You should make any changes there and regenerate it with C-c C-v t
#+end_src
Turn off the mouse interface early in startup to avoid momentary display.

#+begin_src emacs-lisp :tangle early-init.el
(if (fboundp 'menu-bar-mode) (menu-bar-mode 1))
(if (fboundp 'tool-bar-mode) (tool-bar-mode -1))
(if (fboundp 'scroll-bar-mode) (scroll-bar-mode -1))
#+end_src

Turn off the startup screen.

#+begin_src emacs-lisp :tangle early-init.el
(setq inhibit-startup-message t)
#+end_src

Go full screen!

#+begin_src emacs-lisp :tangle early-init.el
(when (fboundp 'toggle-frame-maximized)
  (toggle-frame-maximized))
#+end_src

I don't have a "file containing site-wide run-time initializations."

#+begin_src emacs-lisp :tangle early-init.el
(setq site-run-file nil)
#+end_src

And finally, don't garbage clean so often.

#+begin_src emacs-lisp :tangle early-init.el
(setq gc-cons-threshold 100000000)
#+end_src

Everything from here on is in the regular init file.

* Starting up

# user-emacs-directory is ~/.emacs.d/

** Debug?

Turn this on when needed.

#+begin_src emacs-lisp
;; (setq debug-on-error t)
#+end_src

** Packages

First, set up the MELPA archive.

#+begin_src emacs-lisp :tangle init.el
(require 'package)
(add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t)

(setq package-archive-priorities
      '(("melpa" . 20)
	("gnu" . 10)))
#+end_src

I use John Wiegley's great [[https://github.com/jwiegley/use-package][use-package]] everywhere I can to handle the packages I want.  If it's not installed, get it right off.

#+begin_src emacs-lisp
(unless (package-installed-p 'use-package)
  (package-refresh-contents)
  (package-install 'use-package))

(eval-when-compile
  (require 'use-package))
#+end_src

If I want a package, install it automatically.

#+begin_src emacs-lisp
(setq use-package-always-ensure t)
#+end_src

I'm not sure why I have ~diminish~ and ~bind-key~ in here, but I had a note that says they go with ~use-package.~

#+begin_src emacs-lisp
(use-package diminish)
(use-package bind-key)
#+end_src

** Emacs server

Run the server; now I can load any file into Emacs with ~emacsclient file~ (or, as I have it aliased, ~e file~.)

#+begin_src emacs-lisp
(server-mode)
#+end_src

* Internals and technical stuff

[[http://tsengf.blogspot.ca/2011/06/disable-byte-compile-warning-in-emacs.html][Disable byte-compile warnings]], which I don't care about.

#+begin_src emacs-lisp
(setq byte-compile-warnings '(not nresolved
                                  free-vars
                                  callargs
                                  redefine
                                  obsolete
                                  noruntime
                                  cl-functions
                                  interactive-only
                                  ))
#+end_src

Keep custom settings in separate file.  If =custom.el= doesn't exist, create an empty file (for starting from scratch).

#+begin_src emacs-lisp
(setq custom-file "~/.custom.el")
(unless (file-exists-p custom-file)
  (write-region "" nil custom-file))
(load custom-file)
#+end_src

Install [[https://github.com/jwiegley/emacs-async][async]], which some packages need.

#+begin_src emacs-lisp
(use-package async
  :defer t
  :config
  (dired-async-mode 1)
  )
#+end_src

* Help

[[https://github.com/justbur/emacs-which-key][which-key]] "is a minor mode for Emacs that displays the key bindings following your currently entered incomplete command (a prefix) in a popup."  Very handy:  start a command and wait a second and it will show you all the possible completions.

#+begin_src emacs-lisp
(use-package which-key
  :diminish which-key-mode
  :config
  (which-key-mode)
  )
#+end_src

* Minor one-line customizations

Sentences do not need double spaces to end.  But it's better when they do, of course.

#+begin_src emacs-lisp
(set-default 'sentence-end-double-space nil)
#+end_src

Calendar weeks start on Monday.

#+begin_src emacs-lisp
(setq calendar-week-start-day 1)
#+end_src

I don't want to type in "yes" or "no"---I want y/n.

#+begin_src emacs-lisp
(fset 'yes-or-no-p 'y-or-n-p)
#+end_src

Lower the time to echo keystrokes.

#+begin_src emacs-lisp
(setq echo-keystrokes 0.1)
#+end_src

Never use an X dialog box; use the minibuffer instead.

#+begin_src emacs-lisp
(setq use-dialog-box nil)
#+end_src

* Appearance

** Little tweaks

Proper line wrapping.

#+begin_src emacs-lisp
(global-visual-line-mode 1)
#+end_src

Subtly highlight the current line.

#+begin_src emacs-lisp
(global-hl-line-mode 1)
;; And set its colour
;; (set-face-background hl-line-face "#efefef")
#+end_src

** Pointer

Make the cursor a thin horizontal bar, not a block (but I still like it blinking).  It can be ='bar= or ='box= or ='(hbar . 3)= etc.

#+begin_src emacs-lisp
(set-default 'cursor-type '(bar . 2))
#+end_src

** Faces and fonts

My home and work machines have different screen resolutions, so the font height needs to be different.  It's measured in 0.1 points, so 130 is 13 pt.  I need it a little bigger on my work machine.

#+begin_src emacs-lisp
(setq wtd-ubuntu-mono-height 130)
;; (setq wtd/fira-code-height 120)
(when (string= (system-name) "work")
  (setq wtd-ubuntu-mono-height 160)
  ;; (setq wtd-fira-code-height 160)
  )
#+end_src

I use [[https://en.wikipedia.org/wiki/Ubuntu_(typeface)][Ubuntu]] Mono in Emacs and in terminal windows, so it all looks the same.  I used [[https://en.wikipedia.org/wiki/Fira_(typeface)#Fira_Code][Fira]] Code for a little while, and it was very nice.  Elsewhere I use [[https://en.wikipedia.org/wiki/Baskerville][Baskerville]] as much as possible.

#+begin_src emacs-lisp
(set-face-attribute 'default nil :font "Ubuntu Mono" :height wtd-ubuntu-mono-height)
;; (set-face-attribute 'default nil :font "Fira Code" :height wtd-fira-code-height)
;; (set-face-attribute 'variable-pitch nil :family "Baskervald ADF Std" :height wtd-font-height)
#+end_src

This could help improve performance with Unicode symbols.

#+begin_src emacs-lisp
(setq inhibit-compacting-font-caches t)
#+end_src

Sometimes I try [[https://gitlab.com/jabranham/mixed-pitch][mixed-pitch]], but I never stick with it, so this is turned off.  Fixed pitch is best for me.

 #+begin_src emacs-lisp :tangle no
 (use-package mixed-pitch
   :diminish
   :hook
   (org-mode markdown-mode)
   :config
   (set-face-attribute 'variable-pitch nil :family "Baskervald ADF Std" :height 200)
   )
 #+end_src

Always do font-locking.

#+begin_src emacs-lisp
(setq font-lock-maximum-decoration t)
#+end_src

Prettify symbols!  This is very nice.

#+begin_src emacs-lisp
(global-prettify-symbols-mode +1)
(setq prettify-symbols-unprettify-at-point 'right-edge)
#+end_src

** Unicode and UTF-8

[[https://github.com/purcell/list-unicode-display][list-unicode-display]] "provides a command which searches for Unicode characters by name, and displays a list of matching characters with their names in a buffer."  Similar to ~C-c 8~ in Counsel, which gives an interactive search for Unicode characters.

#+begin_src emacs-lisp
(use-package list-unicode-display)
#+end_src

UTF-8 everywhere (surely this is overkill?).

#+begin_src emacs-lisp
(set-language-environment "UTF-8")
(setq locale-coding-system 'utf-8)
(set-terminal-coding-system 'utf-8)
(set-keyboard-coding-system 'utf-8)
(set-selection-coding-system 'utf-8)
(prefer-coding-system 'utf-8)
#+end_src

** Parentheses

Use [[https://github.com/Fuco1/smartparens/][smartparens]] to handle parentheses.

#+begin_src emacs-lisp
(use-package smartparens
  :diminish smartparens-mode
  :config
  (require 'smartparens-config)
  (smartparens-global-mode t)
  (show-smartparens-global-mode t)
  `(sp-show-pair-match-face :inverse-video t :bold t)
  `(sp-show-pair-mismatch-face :inverse-video t :bold t)
  )
#+end_src

** Indenting

Tabs have four spaces.  Eight is wrong.

#+begin_src emacs-lisp
(setq tab-width 4)
#+end_src

=electric-indent-mode= is built in, but I don't turn it on, because ...

#+begin_src emacs-lisp :tangle no
(electric-indent-mode 1)
#+end_src

... I use [[https://github.com/Malabarba/aggressive-indent-mode][aggressive-indent-mode]], which is indeed aggressive, but very handy.

#+begin_src emacs-lisp
(use-package aggressive-indent
  :diminish aggressive-indent-mode ;; "‚Üí"
  :config
  (global-aggressive-indent-mode 1)
  (add-to-list 'aggressive-indent-excluded-modes 'html-mode)
  )
#+end_src

Use [[https://github.com/zk-phi/indent-guide][indent-guide]] to show vertical lines on indented blocks, which helps make it clear what is where.

#+begin_src emacs-lisp
(use-package indent-guide
  :diminish
  :config
  (indent-guide-global-mode)
  )
#+end_src

** Solarized theme

Bozhidar Batsov's [[https://github.com/bbatsov/solarized-emacs][Solarized theme for Emacs]] is smooth and easy on the eye.

#+begin_src emacs-lisp
(use-package solarized-theme
  :config
  ;; Stop the theme from messing up Org headlines and using variable pitch everywhere.
  (setq solarized-use-variable-pitch nil
	solarized-scale-org-headlines nil)
  (setq x-underline-at-descent-line t) ;; Try this out.
  (load-theme 'solarized-dark t)
  )
#+end_src

** Mode line

Include the size of the file in the mode line.

#+begin_src emacs-lisp
(size-indication-mode t)
#+end_src

Also show which column I'm in.

#+begin_src emacs-lisp
(column-number-mode t)
#+end_src

Fancy up the mode line with [[https://github.com/milkypostman/powerline/][powerline]].  Sticking with the default seems to be nice enough for me.

#+begin_src emacs-lisp
(use-package powerline
  :config
  (powerline-default-theme)
  )
#+end_src

I don't like a crowded mode line, and for most modes I don't want it to show the mode is active, so I use ~:diminish~ when packages are installed with ~use-package~.  That doesn't get everything, so I need to specify some here.

#+begin_src emacs-lisp
(diminish 'abbrev-mode)
(diminish 'emacs-lisp-d-mode " ‚ìÅ")
#+end_src

And here's a list of various other modes I hide a different way.

#+begin_src emacs-lisp
(eval-after-load "autorevert" '(diminish 'auto-revert-mode))
(eval-after-load "eldoc" '(diminish 'eldoc-mode))
;; (eval-after-load "flymake" '(diminish 'flymake-mode))
(eval-after-load "flyspell" '(diminish 'flyspell-mode ""))
;;(eval-after-load "magit" '(diminish 'magit-auto-revert-mode))
(eval-after-load "org-indent" '(diminish 'org-indent-mode)) ;; ‚Üí
(eval-after-load "outline" '(diminish 'outline-minor-mode))
(eval-after-load "rainbow-mode" '(diminish 'rainbow-mode))
(eval-after-load "simple" '(diminish 'visual-line-mode))
(eval-after-load "smerge-mode" '(diminish 'smerge-mode))
(eval-after-load "subword" '(diminish 'subword-mode))
#+end_src

* Sessions, buffers and windows

** Regions

I'm old enough to be able to use ~narrow-to-region~.

#+begin_src emacs-lisp
(put 'narrow-to-region 'disabled nil)
#+end_src

This is another [[https://endlessparentheses.com/emacs-narrow-or-widen-dwim.html][borrowing from Arthur Malabarba]].  ~C-x w~ narrows or widens the region, as appropriate.  This is beautiful magic in Org.

#+begin_src emacs-lisp
(defun narrow-or-widen-dwim (p)
  "Widen if buffer is narrowed, narrow-dwim otherwise.
Dwim means: region, org-src-block, org-subtree, or defun,
whichever applies first. Narrowing to org-src-block actually
calls `org-edit-src-code'.

With prefix P, don't widen, just narrow even if buffer is
already narrowed."
  (interactive "P")
  (declare (interactive-only))
  (cond ((and (buffer-narrowed-p) (not p)) (widen))
        ((region-active-p)
         (narrow-to-region (region-beginning) (region-end)))
        ((derived-mode-p 'org-mode)
         ;; `org-edit-src-code' is not a real narrowing
         ;; command. Remove this first conditional if you
         ;; don't want it.
         (cond ((ignore-errors (org-edit-src-code))
                (delete-other-windows))
               ((ignore-errors (org-narrow-to-block) t))
               (t (org-narrow-to-subtree))))
        ((derived-mode-p 'latex-mode)
         (LaTeX-narrow-to-environment))
        (t (narrow-to-defun))))

(define-key ctl-x-map "w" #'narrow-or-widen-dwim)
(eval-after-load 'latex '(define-key LaTeX-mode-map "\C-xw" nil))
#+end_src

wrap-region to wrap regions in * or / etc.  Extra lines taken from [[http://pragmaticemacs.com/emacs/wrap-text-in-custom-characters/][Wrap text in custom characters]].

#+begin_src emacs-lisp
(use-package wrap-region
  :defer t
  :diminish wrap-region-mode
  :config
  ;; (wrap-region-mode t)
  (wrap-region-add-wrappers
   '(("*" "*" nil org-mode)
     ("~" "~" nil org-mode)
     ("/" "/" nil org-mode)
     ("=" "=" "+" org-mode)
     ("_" "_" nil org-mode)
     ("$" "$" nil (org-mode latex-mode))))
  :init
  (add-hook 'org-mode-hook 'wrap-region-mode)
  (add-hook 'latex-mode-hook 'wrap-region-mode)
  )
#+end_src

[[https://github.com/magnars/expand-region.el][Expand-region]] is kind of magical.  ~C-=~ successively expands the region with great intelligence.

#+begin_src emacs-lisp
(use-package expand-region
  :defer t
  :init
  (global-set-key (kbd "C-=") 'er/expand-region)
  )
#+end_src

** Buffers

Remember all (well, almost) the buffers I have open.

#+begin_src emacs-lisp
(desktop-save-mode 1)
(setq history-length 50)
(setq desktop-buffers-not-to-save
      (concat "\\("
	      "^nn\\.a[0-9]+\\|\\.log\\|(ftp)\\|^tags\\|^TAGS"
	      "\\|\\.emacs.*\\|\\.diary\\|elpa\/*\\|\\.bbdb"
	      "\\)$"))
(add-to-list 'desktop-modes-not-to-save 'dired-mode)
(add-to-list 'desktop-modes-not-to-save 'Info-mode)
(add-to-list 'desktop-modes-not-to-save 'info-lookup-mode)
(add-to-list 'desktop-modes-not-to-save 'fundamental-mode)
#+end_src

Add parts of each file's directory to the buffer name if not unique.

#+begin_src emacs-lisp
(require 'uniquify)
(setq uniquify-buffer-name-style 'forward)
#+end_src

I don't use [[https://github.com/bbatsov/projectile][Projectile]] much, but I like to have it around.  I think it's perhaps better suited for bigger projects than I work on.

#+begin_src emacs-lisp
(use-package projectile
  :config
  (projectile-mode)
  (define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map)
  (setq projectile-mode-line-function '(lambda () (format " ·¥®[%s]" (projectile-project-name))))
  )
#+end_src

A few things about the scratch buffer.  By default it's in lisp-interaction-mode by default, but I use Org more, so make it that.

#+begin_src emacs-lisp
(setq initial-major-mode 'org-mode)
(setq initial-scratch-message "")
#+end_src

Use ~C-c b~ to create a new scratch buffer.

#+begin_src emacs-lisp
(defun create-scratch-buffer nil
  "Create a new scratch buffer to work in (could be *scratch* - *scratchX*)."
  (interactive)
  (let ((n 0)
	bufname)
    (while (progn
	     (setq bufname (concat "*scratch"
				   (if (= n 0) "" (int-to-string n))
				   "*"))
	     (setq n (1+ n))
	     (get-buffer bufname)))
    (switch-to-buffer (get-buffer-create bufname))
    (org-mode)))
(global-set-key (kbd "C-c b") 'create-scratch-buffer)
#+end_src

When I want to kill a buffer, it's always the current one, so don't ask.  ([[http://pragmaticemacs.com/emacs/dont-kill-buffer-kill-this-buffer-instead/][Source]].)

#+begin_src emacs-lisp
(defun wtd/kill-this-buffer ()
  "Kill the current buffer."
  (interactive)
  (kill-buffer (current-buffer)))

(global-set-key (kbd "C-x k") 'wtd/kill-this-buffer)
#+end_src

~ibuffer~ is a nicer way of showing the buffer list (~C-x C-b~).  It's built in.  Alias the usual buffer list command to =ibuffer=.

#+begin_src emacs-lisp
(defalias 'list-buffers 'ibuffer)
#+end_src

Set up some default groups so that files are grouped by type (or location).

#+begin_src emacs-lisp
(setq ibuffer-saved-filter-groups
      (quote (("default"
 	       ("dired" (mode . dired-mode))
 	       ("emacs" (or
 			 (name . "^\\*scratch\\*$")
 			 (name . "^\\*Messages\\*$")))
	       ))))
#+end_src

For [[https://github.com/purcell/ibuffer-projectile][ibuffer-projectile]].

#+begin_src emacs-lisp
(use-package ibuffer-projectile
  :defer t
  :init
  (add-hook 'ibuffer-hook
	    (lambda ()
	      (ibuffer-projectile-set-filter-groups)
	      (unless (eq ibuffer-sorting-mode 'alphabetic)
		((insert) buffer-do-sort-by-alphabetic))))
  )
#+end_src

** Windows

Split the window horizontally, not vertically (I prefer side-by-side with wider screens).

#+begin_src emacs-lisp
(setq split-height-threshold nil)
(setq split-width-threshold 0)
#+end_src

Make window splitting easier: ~C-x 2~ for vertical split, ~C-x 3~ for horizontal.

#+begin_src emacs-lisp
(defun wtd/vsplit-last-buffer (PREFIX)
  "Split the window vertically and display the previous buffer.  By default, switch to that new window; with PREFIX, stay where you are."
  (interactive "p")
  (split-window-vertically)
  (other-window 1 nil)
  (unless prefix
    (switch-to-next-buffer)))

(defun wtd/hsplit-last-buffer (PREFIX)
  "Split the window horizontally and display the previous buffer.  By default, switch to that new window; with PREFIX, stay where you are."
  (interactive "p")
  (split-window-horizontally)
  (other-window 1 nil)
  (unless prefix (switch-to-next-buffer)))

(global-set-key (kbd "C-x 2") 'wtd/vsplit-last-buffer)
(global-set-key (kbd "C-x 3") 'wtd/hsplit-last-buffer)
#+end_src

Use ~C-c left~ or ~C-c right~ to go back and forth in window configurations.

#+begin_src emacs-lisp
(winner-mode t)
#+end_src

Or use ~M-o~ as a shortcut for ~other-window~ instead of the default ~C-x o~, which is too long.  This makes it much easier to toggle between windows.

#+begin_src emacs-lisp
(global-set-key (kbd "M-o") 'other-window)
#+end_src

Use [[https://depp.brause.cc/eyebrowse/][eyebrowse]] to manage window configurations.  I keep forgetting about this but there are times when it would be really handy.

#+begin_quote
You start with your current window config on slot 1. Once you hit ~C-c C-w 2~, you will see the modeline indicator appearing and showing slot 1 and 2 with slot 2 slightly emphasized. Slot 1 has been saved automatically for you and contains your last window config. Do something meaningful like a window split, then hit ~C-c C-w 1~. The window config on slot 2 is saved and the window config from slot 1 is loaded. Try switching back and forth between them with ~C-c C-w '~ to get a feeling for how subsequent window manipulations are handled.
#+end_quote

#+begin_src emacs-lisp
(use-package eyebrowse
  :diminish eyebrowse-mode
  :init (eyebrowse-setup-opinionated-keys)
  :config
  (eyebrowse-mode t)
  ;; (setq eyebrowse-new-workspace t)
  (setq eyebrowse-wrap-around t)
  )
#+end_src

** Sessions

When I close a session, save exactly where I was in the files.

#+begin_src emacs-lisp
(require 'saveplace)
(setq save-place-file (expand-file-name ".places" user-emacs-directory))
(save-place-mode)
#+end_src

* Minibuffer

** amx

[[https://github.com/DarwinAwardWinner/amx/][amx]] is a replace for ~M-x~.

TODO:  What happens if I turn this off?

#+begin_src emacs-lisp
(use-package amx
  :requires helm
  :after ivy
  :custom
  (amx-backend 'ivy)
  )
#+end_src

** ivy, swiper and counsel

;; https://github.com/abo-abo/swiper
;; See also http://irreal.org/blog/?p=6419

#+begin_src emacs-lisp
(use-package counsel)
#+end_src

#+begin_src emacs-lisp
(use-package ivy
  :diminish
  :config
  (ivy-mode 1)
  (setq ivy-use-virtual-buffers t)
  (setq ivy-count-format "%d/%d ")
  )
#+end_src

#+begin_src emacs-lisp
(use-package swiper
  :after ivy
  :bind (("C-s" . swiper)
         ("C-c C-r" . ivy-resume)
         ("M-x" . counsel-M-x)
         ("C-x C-f" . counsel-find-file)
         ("C-M-i" . complete-symbol)
         ("C-." . counsel-imenu)
         ("C-c 8" . counsel-unicode-char)
         ("C-c g" . counsel-git)
         ("C-c k" . counsel-ag)
         ("C-c v" . ivy-push-view)
         ("C-c V" . ivy-pop-view)
         ("M-y" . counsel-yank-pop))
  )
#+end_src

** imenu

#+begin_src emacs-lisp
(global-set-key (kbd "M-i") 'imenu)
#+end_src

* Command launchers

These key mappings make it easier to run common things.  I learned about this from Arthur Malabarba's [[http://endlessparentheses.com/launcher-keymap-for-standalone-features.html][Launcher keymap for standalone features]].

#+begin_src emacs-lisp
(define-prefix-command 'launcher-map)
(define-key ctl-x-map "l" 'launcher-map)
(define-key launcher-map "c" #'calculator) ; calc is too much
(define-key launcher-map "g" #'magit-status)
(define-key launcher-map "l" #'goto-line)
(define-key launcher-map "m" #'mc/edit-lines)
(define-key launcher-map "p" #'list-packages)
(define-key launcher-map "s" #'eshell)
(define-key launcher-map "u" #'magit-pull-from-upstream)
(define-key launcher-map "w" #'count-words-region)
#+end_src

* Editing

** Scrolling

Scroll by one line at a time.

#+begin_src emacs-lisp
(setq scroll-conservatively 10000)
#+end_src

A tip [[https://emacs.stackexchange.com/a/28746/145][found on Stack Exchange]] to stop scrolling from slowing things down.

#+begin_src emacs-lisp
(setq auto-window-vscroll nil)
#+end_src

** Whitespace

Remove trailing whitespace (at the end of a file) automatically.

#+begin_src emacs-lisp
(add-hook 'before-save-hook 'delete-trailing-whitespace)
#+end_src

But it should end with a newline, so if there isn't one there, add it.

#+begin_src emacs-lisp
(setq require-final-newline t)
#+end_src

But down-arrow at the end of a file shouldn't add in a new line.

#+begin_src emacs-lisp
(setq next-line-add-newlines nil)
#+end_src

If there are any empty lines at the end of a buffer, show them (but they will disappear on saving, because of the above).

#+begin_src emacs-lisp
(set-default 'indicate-empty-lines t)
#+end_src

** Colours

[[https://elpa.gnu.org/packages/rainbow-mode.html][rainbow-mode]] is a handy little helper.  "All strings representing colors will be highlighted with the color they represent."

#+begin_src emacs-lisp
(use-package rainbow-mode
  :init
  (add-hook 'prog-mode-hook 'rainbow-mode)
  :config
  (rainbow-mode t) ;; #0af
  )
#+end_src

** Other editing

Let me upcase or downcase a region, which is disabled by default.

#+begin_src emacs-lisp
(put 'downcase-region 'disabled nil)
(put 'upcase-region 'disabled nil)
#+end_src

I don't use multiple-cursors much ... but I should.

#+begin_src emacs-lisp
(use-package multiple-cursors)
#+end_src

Open a new line above or below the current one, even if the cursor is mid-sentence.

#+begin_src emacs-lisp
(defun open-line-below ()
  (interactive)
  (end-of-line)
  (newline)
  (indent-for-tab-command))

(defun open-line-above ()
  (interactive)
  (beginning-of-line)
  (newline)
  (forward-line -1)
  (indent-for-tab-command))

(global-set-key (kbd "<C-return>") 'open-line-below)
(global-set-key (kbd "<C-S-return>") 'open-line-above)
#+end_src

Move an entire line up or down with C-S-up or C-S-down.

#+begin_src emacs-lisp
(defun move-line-down ()
  (interactive)
  (let ((col (current-column)))
    (save-excursion
      (forward-line)
      (transpose-lines 1))
    (forward-line)
    (move-to-column col)))

(defun move-line-up ()
  (interactive)
  (let ((col (current-column)))
    (save-excursion
      (forward-line)
      (transpose-lines -1))
    (move-to-column col)))

(global-set-key (kbd "<C-S-down>") 'move-line-down)
(global-set-key (kbd "<C-S-up>") 'move-line-up)
#+end_src

Join the following line onto this one.  Good for reformatting.

#+begin_src emacs-lisp
(global-set-key (kbd "M-j")
            (lambda ()
                  (interactive)
                  (join-line -1)))
#+end_src

** Searching

#+begin_src emacs-lisp
;; Make searches case insensitive.
(setq case-fold-search nil)

;; Turn on highlighting for search strings.
(setq search-highlight t)
#+end_src

[[https://github.com/syohex/emacs-anzu][anzu-mode]] provides a "minor mode which display current point and  total matched in various search mode."

#+begin_src emacs-lisp
(use-package anzu
  :diminish anzu-mode
  :config
  (global-anzu-mode t)
  (global-set-key (kbd "M-%") 'anzu-query-replace)
  (global-set-key (kbd "C-M-%") 'anzu-query-replace-regexp)
  )
#+end_src

I was getting errors about exceeding the defaults on both of these.

#+begin_src emacs-lisp
(setq max-specpdl-size 50000)
(setq max-lisp-eval-depth 25000)
#+end_src

** Copying and pasting

Highlight marked text.

#+begin_src emacs-lisp
(transient-mark-mode t)
#+end_src

Remove text in active region if inserting text.

#+begin_src emacs-lisp
(delete-selection-mode 1)
#+end_src

[[https://github.com/k-talo/volatile-highlights.el][volatile-highlights]] temporarily highlights what you've just pasted in.  When you move the pointer, it disappears.

#+begin_src emacs-lisp
(use-package volatile-highlights
  :init (volatile-highlights-mode t)
  :diminish volatile-highlights-mode
  :config
  (vhl/define-extension 'undo-tree 'undo-tree-yank 'undo-tree-move)
  (vhl/install-extension 'undo-tree)
  )
#+end_src

Enable cutting/pasting and putting results into the X clipboard

#+begin_src emacs-lisp
(global-set-key [(shift delete)] 'clipboard-kill-region)
(global-set-key [(control insert)] 'clipboard-kill-ring-save)
(global-set-key [(shift insert)] 'clipboard-yank)
#+end_src

Allow pasting selection outside of Emacs

#+begin_src emacs-lisp
(setq select-enable-clipboard t)
#+end_src

~M-backspace~ is ~backward-word-kill~, and ~C-backspace~ is bound to that by default. Change that to ~backword-kill-line~ so it deletes from the point to the beginning of the line.

#+begin_src emacs-lisp
(global-set-key (kbd "C-<backspace>") (lambda ()
					(interactive)
					(kill-line 0)))
#+end_src

** Spelling

Use flyspell for spell-checking in comments in programs.

#+begin_src emacs-lisp
(add-hook 'prog-mode-hook #'flyspell-prog-mode)
#+end_src

** Expansions and abbreviations

*** abbrev

#+begin_src emacs-lisp
(setq-default abbrev-mode t)

;; Where to store the abbrevs.
(setq abbrev-file-name "~/.emacs.d/abbrev_defs")

;; Save abbrevs when files are saved
(setq save-abbrevs t)

;; Don't ask whether to save abbrevs when quitting Emacs
(setq save-abbrevs nil)
#+end_src

*** company

#+begin_src emacs-lisp
(use-package company
  ;; https://company-mode.github.io/
  :diminish company-mode
  :init
  ;; (global-company-mode)
  (add-hook 'ruby-mode-hook 'company-mode)
  :diminish company-mode
  :config
  (setq company-minimum-prefix-length 1)
  (setq company-idle-delay 1)
  (setq company-tooltip-align-annotations 't)          ; align annotations to the right tooltip border
  (setq company-begin-commands '(self-insert-command)) ; start autocompletion only after typing
  (setq company-tooltip-limit 20)                      ; bigger popup window
  )
#+end_src

*** yasnippets

https://joaotavora.github.io/yasnippet/

#+begin_src emacs-lisp
(use-package yasnippet
  :diminish yas-minor-mode
  :config
  (use-package yasnippet-snippets)
  (yas-global-mode 1)
  )
#+end_src

** Undoing

[[https://gitlab.com/tsc25/undo-tree][undo-tree]]

#+begin_src emacs-lisp
(use-package undo-tree
  :diminish undo-tree-mode
  :config
  (global-undo-tree-mode)
  :custom
  (undo-tree-visualizer-timestamps t)
  (undo-tree-visualizer-diff t)
  )
#+end_src

* File management

Keep a list of recently opened files

#+begin_src emacs-lisp
(require 'recentf)
(recentf-mode 1)
(setq recentf-save-file "~/.recentf")
#+end_src

Rebind FFAP's =find-file-at-point= so it goes with =C-x f= (usually =find-file=), so I don't have to use =C-x C-f= (its default binding).  Saves time.

#+begin_src emacs-lisp
(global-set-key (kbd "C-x f") 'find-file-at-point)
#+end_src

Refresh buffers when files change.  But don't worry:  "Auto Revert will not revert a buffer if it has unsaved changes, or if its file on disk is deleted or renamed."

#+begin_src emacs-lisp
(global-auto-revert-mode t)
#+end_src

Don't show uninteresting files in Emacs completion window ([[https://stackoverflow.com/a/1732081/854346][from Stack Overflow]]).  When the buffer of possible files open, it shows all matches.  If I'm looking for ~foo.ext~ and hit ~C-x C-f fo TAB~ it will show ~foo.ext~ and ~foo.ext~~.  Because =~= is in completion-ignored-extensions it won't try to open ~foo.ext~~, but I'd rather not see it in the first place.

#+begin_src emacs-lisp
(defadvice completion--file-name-table (after ignoring-backups-f-n-completion activate)
  "Filter out results when they match `completion-ignored-extensions'."
  (let ((res ad-return-value))
    (if (and (listp res)
	     (stringp (car res))
	     (cdr res))                 ; length > 1, don't ignore sole match
	(setq ad-return-value
              (completion-pcm--filename-try-filter res)))))
#+end_src

** dired

Auto refresh dired, but be quiet about it.

#+begin_src emacs-lisp
(setq global-auto-revert-non-file-buffers t)
#+end_src

Tell dired how to handle some file types.

#+begin_src emacs-lisp
(setq dired-guess-shell-alist-user
      '(("\\.pdf\\'" "evince")
	("\\.tex\\'" "pdflatex")
	("\\.ods\\'\\|\\.xlsx?\\'\\|\\.docx?\\'\\|\\.csv\\'" "libreoffice")))
#+end_src

~C-x C-j~ (~M-x dired-jump~) instantly goes to the current file's position in a dired buffer.  No need to open up a dired buffer and move the pointer.

#+begin_src emacs-lisp
(require 'dired-x)
#+end_src

Emacs 24.4 defaults to an =ls -1= (dash one) view, not =ls -l= ( dash ell), but I want the long format.

#+begin_src emacs-lisp
(setq diredp-hide-details-initially-flag nil)
#+end_src

"In Dired, visit this file or directory instead of the Dired buffer."  Prevents buffers littering up things when moving around in Dired.

#+begin_src emacs-lisp
(put 'dired-find-alternate-file 'disabled nil)
#+end_src

Make it easier to move and copy files across windows.

#+begin_src emacs-lisp
(setq dired-dwim-target t)
#+end_src

=dired+= has got some wild colours by default. This turns that off, but leaves the settings at maximum (the default) for everything else.

#+begin_src emacs-lisp
(setq font-lock-maximum-decoration (quote ((dired-mode) (t . t))))
#+end_src

** Rename or delete the current buffer and its file

File management shortcuts from [[https://github.com/bodil/emacs.d][Bodil Stokke]]'s setup.  These are both really handy.

+ ~C-x C-k~ to delete the file being edited and kill the buffer
+ ~C-x C-r~ to rename the file being edited end the current buffer

#+begin_src emacs-lisp
(defun delete-current-buffer-file ()
  "Remove file connected to current buffer and kills buffer."
  (interactive)
  (let ((filename (buffer-file-name))
        (buffer (current-buffer))
        (name (buffer-name)))
    (if (not (and filename (file-exists-p filename)))
        (ido-kill-buffer)
      (when (yes-or-no-p "Are you sure you want to remove this file? ")
        (delete-file filename)
        (kill-buffer buffer)
        (message "File '%s' successfully removed" filename)))))
(global-set-key (kbd "C-x C-k") 'delete-current-buffer-file)
#+end_src

#+begin_src emacs-lisp
(defun rename-current-buffer-file ()
  "Rename current buffer and file it is visiting."
  (interactive)
  (let ((name (buffer-name))
        (filename (buffer-file-name)))
    (if (not (and filename (file-exists-p filename)))
        (error "Buffer '%s' is not visiting a file!" name)
      (let ((new-name (read-file-name "New name: " filename)))
        (if (get-buffer new-name)
            (error "A buffer named '%s' already exists!" new-name)
          (rename-file filename new-name 1)
          (rename-buffer new-name)
          (set-visited-file-name new-name)
          (set-buffer-modified-p nil)
          (message "File '%s' successfully renamed to '%s'"
                   name (file-name-nondirectory new-name)))))))
(global-set-key (kbd "C-x C-r") 'rename-current-buffer-file)
#+end_src

* File types

** Markdown

#+begin_src emacs-lisp
(use-package markdown-mode
  :commands (markdown-mode gfm-mode)
  :mode (("README\\.md\\'" . gfm-mode)
	 ;; Defined elsewhere: use polymode
         ;; ("\\.md\\'" . markdown-mode)
         ;; ("\\.markdown\\'" . markdown-mode)
	 )
  :init
  ;; (setq markdown-command "multimarkdown")
  (setq markdown-hide-urls t)
  (setq markdown-hide-markup t)
  (setq markdown-url-compose-char "‚ãØ")
  (setq markdown-header-scaling t)
  ;; (add-hook 'markdown-mode-hook 'turn-on-outline-minor-mode)
  (add-hook 'markdown-mode-hook 'turn-on-visual-line-mode)
  )
#+end_src

** nxml for HTML etc.

#+begin_src emacs-lisp
(add-to-list 'auto-mode-alist
	     (cons (concat "\\." (regexp-opt '("xml" "xsd" "sch" "rng" "xslt" "svg" "rss") t) "\\'")
		   'nxml-mode))
(fset 'html-mode 'nxml-mode)

(add-to-list 'hs-special-modes-alist
	     '(nxml-mode
               "<!--\\|<[^/>]*[^/]>"
               "-->\\|</[^/>]*[^/]>"
               "<!--"
               sgml-skip-tag-forward
               nil))
(add-hook 'nxml-mode-hook 'hs-minor-mode)
;; (define-key nxml-mode-map (kbd "C-c h") 'hs-toggle-hiding)

(with-eval-after-load "nxml-mode"
  ;; Need to define this only after nxml-mode has been loaded
  (define-key nxml-mode-map "\C-c h" 'hs-toggle-hiding))
#+end_src

** YAML

#+begin_src emacs-lisp
(use-package yaml-mode
  :init
  (add-to-list 'auto-mode-alist '("\\.yml$" . yaml-mode))
  )
#+end_src


** CSV files

#+begin_src emacs-lisp
(use-package csv-mode)
#+end_src

** JSON

#+begin_src emacs-lisp
(use-package json-mode)
#+end_src

* Git

[[https://magit.vc/][Magit]] will install ~with-editor~ when it goes in.

#+begin_src emacs-lisp
(use-package magit
  ;; :config
  )
#+end_src

[[https://github.com/syohex/emacs-git-gutter][git-gutter]].

#+begin_src emacs-lisp
(use-package git-gutter-fringe
  :diminish git-gutter-mode
  :config
  (global-git-gutter-mode t)
  (setq git-gutter-fr:side 'right-fringe)
  )
#+end_src

* Org

A note I made on 16 April 2013:  "Started using org-mode ... I could really get into this."

I should try [[https://github.com/jkitchin/org-ref][org-ref]].

#+begin_src emacs-lisp
(use-package org
  ;; Use Org's current development branch, pulled down with git
  ;; (http://orgmode.org/org.html#Installation)
  :load-path "/usr/local/src/org-mode/lisp"

  :config
  (setq org-fontify-whole-heading-line t)
  (global-set-key "\C-cl" 'org-store-link)

  ;; Show column headings even when off the top of the screen.
  ;; (setq org-table-header-line-p nil)
  ;; (diminish 'org-table-header-line-mode)

  ;; org-entities displays \alpha etc. as Unicode characters.
  (setq org-pretty-entities t)

  ;; Hide the /italics/ and *bold* markers
  (setq org-hide-emphasis-markers t)

  ;; Better colouring of TODO keywords
  (setq org-todo-keyword-faces
	(quote (
		("TODO" :foreground "SeaGreen" :weight normal)
		("WAITING" :foreground "Purple" :weight normal)
		)))

  (set-face-attribute 'org-link nil :foreground "Steel Blue")

  ;; Make completed items in a checkbox list less noticeable
  ;; https://fuco1.github.io/2017-05-25-Fontify-done-checkbox-items-in-org-mode.html
  (font-lock-add-keywords
   'org-mode
   `(("^[ \t]*\\(?:[-+*]\\|[0-9]+[).]\\)[ \t]+\\(\\(?:\\[@\\(?:start:\\)?[0-9]+\\][ \t]*\\)?\\[\\(?:X\\|\\([0-9]+\\)/\\2\\)\\][^\n]*\n\\)" 1 'org-headline-done prepend))
   'append)

  ;; Hit return on a link to open it in a browser
  (setq org-return-follows-link t)

  ;; Shift and arrow keys to select text works a bit differently in Org.
  (setq org-support-shift-select t)

  ;; Make C-a and C-e understand how headings and tags work
  ;; Seen at http://schenizzle.wordpress.com/2014/03/26/org-mode-ctrl-a-ctrl-e/
  (setq org-special-ctrl-a/e t)

  ;; Visually indent everything nicely, but leave the raw file left-aligned
  (setq org-startup-indented t)

  ;; Never show blank lines in condensed view
  (setq org-cycle-separator-lines 0)

  ;; Fontify Babel blocks nicely
  (setq org-src-fontify-natively t)

  ;; Preserve indentation when tangling source blocks (important for makefiles)
  (setq org-src-preserve-indentation t)

  ;; Allow a) b) c) lists
  (setq org-list-allow-alphabetical t)

  ;; Right-align tags to an indent from the right margin
  ;; (setq org-tags-column (- 50 (window-width)))
  (setq org-tags-column 120)

  ;; "Single keys can be made to execute commands when the cursor is at
  ;; the beginning of a headline, i.e., before the first star."
  (setq org-use-speed-commands t)

  ;; Embed an image with [[file:foo.png]] and then C-c C-x C-v to view
  (setq org-display-inline-images t)

  ;; Show on startup
  (setq org-startup-with-inline-images t)

  ;; How to rearrange things when I edit a source block
  ;; default is regorganize-frame
  (setq org-src-window-setup 'current-window)

  ;; Automatically refresh inline images that are generated from Babel blocks
  (add-hook 'org-babel-after-execute-hook 'org-redisplay-inline-images)

  ;; Display images when a file is loaded (I can always toggle them off if I don't want them)
  (add-hook 'org-mode-hook (lambda () (org-toggle-inline-images)))

  ;; Use LaTeX spell-check
  (add-hook 'org-mode-hook (lambda () (setq ispell-parser 'tex)))

  ;; Use flyspell to check spelling as I go
  (add-hook 'org-mode-hook 'turn-on-flyspell)

  ;; Use C-c d to close all the open drawers in a file
  (defun add-org-close-all-drawers-key ()
    (local-set-key (kbd "C-c d") (lambda () (interactive) (org-cycle-hide-drawers 'all))))
  (add-hook 'org-mode-hook 'add-org-close-all-drawers-key)

  ;; imenu integration
  ;; (add-hook 'org-mode-hook
  ;; (lambda () (imenu-add-to-menubar "Imenu")))

  ;; Preview LaTeX equations in buffers by showing images (C-c C-x C-l)
  ;; Details: http://orgmode.org/worg/org-tutorials/org-latex-preview.html
  ;;  (setq org-preview-latex-default-process 'imagemagick)

  ;; Highlight inline LaTeX
  (setq org-highlight-latex-and-related '(latex))

  ;; Exporting: I will see these export options after C-c C-e
  (setq org-export-backends (quote (html latex md odt))) ;; beamer reveal

  ;; I may need to customize org-html-doctype (default is "xhtml-strict")
  ;; (setq org-html-doctype "html5")

  ;; Turn ' and " into ‚Äòposh‚Äô ‚Äúquotes‚Äù
  (setq org-export-with-smart-quotes t)

  ;; Date format on exports
  ;; (setq org-export-date-timestamp-format "%d %m %Y")

  ;; Use wrap-region
  (add-hook 'org-mode-hook 'wrap-region-mode)

  ;; Footnotes. I want them defined nearby, not at the bottom of the
  ;; document, when I use C-c C-x f.  And I don't want them resorted
  ;; or adjusted without my saying so.
  (setq org-footnote-section nil)
  (setq org-footnote-auto-adjust nil)

  ;; Define my own link abbreviations
  (setq org-link-abbrev-alist
	'(
	  ("DOI" . "http://dx.doi.org/%s")                        ;; Thus [[DOI:10.1108/07378831111138189]]
	  ("WP"  . "https://en.wikipedia.org/wiki/%s")            ;; Thus [[WP:Toronto, Ontario]]
	  ("YUL" . "https://www.library.yorku.ca/find/Record/%s") ;; Thus [[YUL:2935857]]
	  )
	)

  ;; Hooks for prettify-symbols-mode
  ;; See also https://pank.eu/blog/pretty-babel-src-blocks.html for some cool stuff
  ;; And https://github.com/zzamboni/dot-emacs/blob/master/init.org#source-code-blocks
  ;; some stuff I tried out but decided was a bit too much for me.
  (add-hook 'org-mode-hook
 	    (lambda ()
 	      (push '("<=" . ?‚â§) prettify-symbols-alist)
 	      (push '(">=" . ?‚â•) prettify-symbols-alist)
 	      (push '("#+BEGIN_SRC" . ?‚é°) prettify-symbols-alist) ;;  ‚é° ‚û§ üñù ‚ûü ‚û§ ‚úé
 	      (push '("#+END_SRC" . ?‚é£) prettify-symbols-alist) ;; ‚é£ ‚úê
 	      (push '("#+begin_src" . ?‚é°) prettify-symbols-alist)
 	      (push '("#+end_src" . ?‚é£) prettify-symbols-alist)
 	      (push '("#+BEGIN_QUOTE" . ?‚ùù) prettify-symbols-alist)
 	      (push '("#+END_QUOTE" . ?‚ùû) prettify-symbols-alist)
 	      (push '("#+begin_quote" . ?‚ùù) prettify-symbols-alist)
 	      (push '("#+end_quote" . ?‚ùû) prettify-symbols-alist)
 	      ))

  ;; Capturing
  ;; (setq org-default-notes-file "~/org/capture.org") ; Change this when I use it for real
  ;; (define-key global-map "\C-cc" 'org-capture)
  (setq org-capture-templates
	'(
      	  ("w" "Work todo" entry (file+headline "~/york/shared/projects/projects.org" "Tasks") "* TODO %?\n %u\n %a")
      	  ("d" "Work diary" entry (file+datetree "~/york/shared/work-diaries/work-diary.org" "Tasks") "** %?\n %u\n %a")
	  ("n" "Note"      entry (file+datetree "~/org/capture.org")                   "* %?\nEntered on %U\n  %i\n %a"))
	)

  ;; Refiling
  (setq org-refile-targets '(
			     ("~/york/shared/projects/projects.org" :maxlevel . 1)
			     ;; ("~/york/shared/reports/annual/2017-annual-report/denton-2016-2017-annual-report.org" :maxlevel . 2)
			     ))

  ;; org-protocol lets me send URLs from Firefox (and more, but that's all I'm doing)
  ;; See https://stackoverflow.com/questions/7464951/how-to-make-org-protocol-work
  ;; (require 'org-protocol)
  ;; '(setq org-protocol-default-template-key "n")

  ;; Active Babel languages
  ;; See http://orgmode.org/org.html#Languages
  (org-babel-do-load-languages
   'org-babel-load-languages
   '(
     (ditaa . t)
     (dot . t)
     (latex . t)
     (lilypond . t)
     (python . t)
     (R . t)
     (ruby . t)
     (shell . t)
     (sql . t)
     (sqlite . t)
     )
   )

  ;; Source code block appearance
  (set-face-attribute 'org-block-begin-line nil :underline nil)
  (set-face-attribute 'org-block-end-line nil :overline nil)

  ;; Evaluate Babel blocks without asking for confirmation
  (setq org-confirm-babel-evaluate nil)

  ;; In 25 Org started opening exported PDFs in docview, but I prefer
  ;; seeing them externally.
  (delete '("\\.pdf\\'" . default) org-file-apps)
  (add-to-list 'org-file-apps '("\\.pdf\\'" . "evince %s"))

  ;; For this to work, ditaa must be installed
  (setq org-ditaa-jar-path "/usr/share/ditaa/ditaa.jar")

  ;; Change the ellipsis that indicates hidden content
  ;; http://endlessparentheses.com/changing-the-org-mode-ellipsis.html
  (setq org-ellipsis " ‚¨é") ;; ‚§µ ‚Ü¥ ‚¨é ‚§∑
  (set-face-attribute 'org-ellipsis nil :underline nil)

  ;; Make LOGBOOK and such fainter.  Default bold is too loud.
  (face-spec-set 'org-drawer '((t (:foreground "dim gray" :weight normal))))

  ;; (face-spec-set 'org-level-1 '((t (:height 1.05))))
  ;; (face-spec-set 'org-level-2 '((t (:height 1.05))))
  ;; (face-spec-set 'org-level-3 '((t (:height 1.0))))

  ;; Clocking
  (setq org-clock-persist 'history)
  (org-clock-persistence-insinuate)

  )
#+end_src

[[https://github.com/sabof/org-bullets][org-bullets]]

#+begin_src emacs-lisp
(use-package org-bullets
  ;; Possibilities:  ‚óâ ‚óã ‚ú∏ ‚úø ‚ô• ‚óè ‚óá ‚úö ‚úú ‚òØ ‚óÜ ‚ô† ‚ô£ ‚ô¶ ‚ò¢ ‚ùÄ ‚óÜ ‚óñ ‚ñ∂ ‚ñ∫ ‚Ä¢ ‚òÖ ‚ñ∏ or any other Unicode character
  ;; Default is '("‚óâ" "‚óã" "‚ú∏" "‚úø")
  ;; I've used ("‚óâ" "‚óã ""‚ñ∫" "‚Ä¢" "‚Ä¢"))
  :after org
  :config
  (setq org-bullets-bullet-list '("‚ä¢" "‚ãÆ" "‚ã±" "‚ã±" "‚ã±"))
  :hook
  (org-mode . (lambda () (org-bullets-mode 1)))
  )
#+end_src

;; I asked and someone answered on the beta Emacs SE.
;; https://emacs.stackexchange.com/questions/90/how-to-sometimes-but-not-always-add-a-note-to-an-org-todo-state-change
;; This lets me force a note for any state change in TODO workflow.
;; Use C-c C-T (capital T) to make Org ask me for a note, even if the normal workflow doesn't require it.
;; (defun org-todo-force-notes ()
;;   (interactive)
;;   (let ((org-todo-log-states
;;          (mapcar (lambda (state)
;;                    (list state 'note 'time))
;;                  (apply 'append org-todo-sets))))
;;     (call-interactively 'org-todo)))
;; (define-key org-mode-map (kbd "C-c C-S-t") 'org-todo-force-notes)

Ispell should [[http://endlessparentheses.com/ispell-and-org-mode.html][ignore some things in Org files]].

#+begin_src emacs-lisp
(defun endless/org-ispell ()
  "Configure `ispell-skip-region-alist' for `org-mode'."
  (make-local-variable 'ispell-skip-region-alist)
  (add-to-list 'ispell-skip-region-alist '(org-property-drawer-re))
  (add-to-list 'ispell-skip-region-alist '("~" "~"))
  (add-to-list 'ispell-skip-region-alist '("=" "="))
  (add-to-list 'ispell-skip-region-alist '("^#\\+BEGIN_SRC" . "^#\\+END_SRC")))
(add-hook 'org-mode-hook #'endless/org-ispell)
#+end_src

Since I'm using ~C-x n~ to narrow and widen source blocks (see ~narrow-or-widen-dwim~) I don't need to use ~C-c `~ to enter and leave them, so I can use ~C-x C-s~ to save and exit them, which is nice.  [[http://endlessparentheses.com/emacs-narrow-or-widen-dwim.html][Source]].

#+begin_src emacs-lisp
(eval-after-load 'org-src
  '(define-key org-src-mode-map
     "\C-x\C-s" #'org-edit-src-exit))
#+end_src

[[https://emacs.stackexchange.com/a/49068/145][Replace a link with just the descriptive text]].

#+begin_src emacs-lisp
(defun wtd/org-link-delete-link ()
  "Remove the link from an Org link at point and keep only the description."
  (interactive)
  (let ((elem (org-element-context)))
    (if (eq (car elem) 'link)
        (let* ((content-begin (org-element-property :contents-begin elem))
               (content-end  (org-element-property :contents-end elem))
               (link-begin (org-element-property :begin elem))
               (link-end (org-element-property :end elem)))
          (if (and content-begin content-end)
              (let ((content (buffer-substring-no-properties content-begin content-end)))
                (delete-region link-begin link-end)
                (insert content)))))))
#+end_src

[[https://emacs.stackexchange.com/questions/13869/how-to-toggle-org-mode-source-code-block-eval-no-status][Use C-c t to toggle ":eval no|yes" status in source blocks]].

#+begin_src emacs-lisp
(defun org-toggle-src-eval-no ()
  "Toggle ':eval no' on the src block begin line."
  (defun in-src-block-p ()
    "Returns t when the point is inside a source code block"
    (string= "src" (org-in-block-p '("src"))))
  (defun beginning-src ()
    "Find the beginning of the src block"
    (let ((case-fold-search t)) (search-backward "#+BEGIN_SRC")))
  (defun toggle-eval-no ()
    "Handles the toggling of ' :eval no'"
    (save-excursion
      (end-of-line)
      (let ((case-fold-search t)) (search-backward "#+BEGIN_SRC")
	   (if (search-forward " :eval no" (line-end-position) "f")
	       (replace-match "")
	     (insert " :eval no")
	     ))))
  (if (in-src-block-p) (toggle-eval-no)))
(defun add-org-toggle-src-key ()
  (local-set-key (kbd "C-c t") (lambda () (interactive) (org-toggle-src-eval-no))))
(add-hook 'org-mode-hook 'add-org-toggle-src-key)
#+end_src

[[http://endlessparentheses.com/ispell-and-org-mode.html][Stop ispell from looking where it shouldn't]].

#+begin_src emacs-lisp
(defun wtd/org-ispell ()
  "Configure `ispell-skip-region-alist' for `org-mode'."
  (make-local-variable 'ispell-skip-region-alist)
  (add-to-list 'ispell-skip-region-alist '(org-property-drawer-re))
  (add-to-list 'ispell-skip-region-alist '("~" "~"))
  (add-to-list 'ispell-skip-region-alist '("=" "="))
  (add-to-list 'ispell-skip-region-alist '("^#\\+BEGIN_SRC" . "^#\\+END_SRC"))
  (add-to-list 'ispell-skip-region-alist '("^#\\+begin_src" . "^#\\+end_src"))
  (add-to-list 'ispell-skip-region-alist '("^#\\+BEGIN_EXAMPLE ". "#\\+END_EXAMPLE"))
  )
(add-hook 'org-mode-hook #'wtd/org-ispell)
#+end_src

So I can use Memoir as a document class in Org (but I don't).

#+begin_src emacs-lisp :tangle no
(with-eval-after-load 'ox-latex
  (add-to-list 'org-latex-classes
               '("memoir"
                 "\\documentclass{memoir}"
                 ("\\book{%s}" . "\\book*{%s}")
                 ("\\part{%s}" . "\\part*{%s}")
                 ("\\chapter{%s}" . "\\chapter*{%s}")
                 ("\\section{%s}" . "\\section*{%s}")
                 ("\\subsection{%s}" . "\\subsection*{%s}")
                 ("\\subsubsection{%s}" . "\\subsubsection*{%s}")))
  (add-to-list 'org-latex-classes
               '("memoir-chapter+"
                 "\\documentclass{memoir}"
                 ("\\chapter{%s}" . "\\chapter*{%s}")
                 ("\\section{%s}" . "\\section*{%s}")
                 ("\\subsection{%s}" . "\\subsection*{%s}")
                 ("\\subsubsection{%s}" . "\\subsubsection*{%s}")))
  )
#+end_src

* Programming

** General stuff

I sometimes use CamelCase in Ruby and R.

#+begin_src emacs-lisp
(add-hook 'prog-mode-hook 'subword-mode)
#+end_src

Make script files executable automatically

#+begin_src emacs-lisp
(add-hook 'after-save-hook 'executable-make-buffer-file-executable-if-script-p)
#+end_src

** Comint

Settings for command interpreter modes, which I use mostly for R and Ruby.

#+begin_src emacs-lisp
(setq ansi-color-for-comint-mode 'filter)
(setq comint-scroll-to-bottom-on-input t)
(setq comint-scroll-to-bottom-on-output t)
(setq comint-move-point-for-output t)
;;(setq comint-prompt-read-only t)
#+end_src

Colourise the comint buffer.

#+begin_src emacs-lisp
(setq ansi-color-for-comint-mode 'filter)
#+end_src

** Syntax checking

[[https://www.flycheck.org/en/latest/][Flycheck]] for syntax checking.

#+begin_src emacs-lisp
(use-package flycheck
  :init (global-flycheck-mode)
  :diminish flycheck-mode
  :config
  (setq flycheck-global-modes '(not org-mode))
  ;; Could also set :modes to list where I want it.
  )
#+end_src

** Linting

Flycheck and lintr.

#+begin_src emacs-lisp
(setq-default flycheck-lintr-linters
              (concat "with_defaults(line_length_linter(120), "
                      "absolute_paths_linter = NULL, "
		      ;; "camel_case_linter = NULL, "
		      ;; "snake_case_linter = NULL, "
		      "commented_code_linter = NULL)"))
#+end_src

* R and ESS

[[https://ess.r-project.org/][ESS]].

#+begin_src emacs-lisp
(use-package ess
  :config
  (setq ess-smart-S-assign-key nil) ;; Don't let _ turn into <-
  (setq ess-use-auto-complete t) ;; Auto-completion on.
  (setq ess-use-flymake nil) ;; Don't run flymake on ESS buffers
  (setq ess-help-own-frame 'nil) ;; Make all help buffers go into one frame
  (setq inferior-ess-primary-prompt "> ") ;; Fancier prompt (see also ~/.Rprofile) (used to be ‚Ñù>)
  (setq inferior-S-prompt "[]a-zA-Z0-9.[]*\\(?:[>+.] \\)*‚Ñù+> ")
  :init
  (add-hook 'ess-R-post-run-hook 'ess-execute-screen-options) ;; Use the full width of the Emacs frame
  (add-hook 'ess-post-run-hook 'ess-execute-screen-options)
  (add-hook 'ess-mode-hook 'indent-guide-mode)   ;; indent-guide ... very nice
  )
#+end_src

(Don't) Use polymode (see elsewhere) for Rmd files.

#+begin_src emacs-lisp :tangle no
;; (add-to-list 'auto-mode-alist '("\\.Rmd$" . R-mode))
#+end_src

Start R in the current directory.  May need to change dirs with =setwd()= after.

#+begin_src emacs-lisp
(setq ess-ask-for-ess-directory nil)
#+end_src

Display =%>%= as =|=, thanks to =prettify-symbols-mode=.  Really makes R code look nice.

#+begin_src emacs-lisp
(add-hook 'inferior-ess-mode-hook
	  (lambda ()
	    (push '("%>%" . ?|) prettify-symbols-alist)))
#+end_src

I don't know what this does.

#+begin_src emacs-lisp
(setq ess-local-process-name "R")
#+end_src

This stops comments from flying all the way over to the right, and makes =%>%= chains indent nicely (if the newline is after the pipe).

#+begin_src emacs-lisp
(add-hook 'ess-mode-hook
	  (lambda ()
	    (setq ess-indent-level 4)
	    (setq ess-first-continued-statement-offset 2)
	    (setq ess-continued-statement-offset 0)
	    (setq ess-offset-continued 'straight)
	    (setq ess-brace-offset -4)
            (setq ess-expression-offset 4)
            (setq ess-else-offset 0)
            (setq ess-close-brace-offset 0)
            (setq ess-brace-imaginary-offset 0)
            (setq ess-continued-brace-offset 0)
            (setq ess-arg-function-offset 4)
	    (setq ess-arg-function-offset-new-line '(4))
	    ))
#+end_src

Be more colourful!

#+begin_src emacs-lisp
(setq ess-R-font-lock-keywords
      (quote
       ((ess-R-fl-keyword:modifiers . t)
        (ess-R-fl-keyword:fun-defs . t)
        (ess-R-fl-keyword:keywords . t)
        (ess-R-fl-keyword:assign-ops . t)
        (ess-R-fl-keyword:constants . t)
        (ess-fl-keyword:fun-calls . t)
        (ess-fl-keyword:numbers . t)
        (ess-fl-keyword:operators . t)
        (ess-fl-keyword:delimiters . t)
        (ess-fl-keyword:=)
	(ess-R-fl-keyword:F&T))))
#+end_src

* Ruby

Make sure =ruby-mode= is used in the right places.

#+begin_src emacs-lisp
(autoload 'ruby-mode "ruby-mode" "Mode for editing Ruby")
(add-to-list 'auto-mode-alist '("\\.rb$" . ruby-mode))
(add-to-list 'interpreter-mode-alist '("ruby" . ruby-mode))
#+end_src

I use [[https://github.com/rbenv/rbenv][rbenv]], so Emacs needs to know about it.

#+begin_src emacs-lisp
(use-package rbenv
  :hook (ruby-mode . global-rbenv-mode)
  :config
  (setq rbenv-show-active-ruby-in-modeline nil)
  (setq rbenv-modeline-function 'rbenv--modeline-plain)
  (rbenv-use-global)
  )
#+end_src

Open up ~irb~ with ~M-x inf-ruby~ or ~C-c C-s~ from a Ruby buffer.

#+begin_src emacs-lisp
(use-package inf-ruby
  :hook (ruby-mode . inf-ruby-minor-mode)
  :config
  (autoload 'inf-ruby "inf-ruby" "Run an inferior Ruby process" t)
  (add-to-list 'inf-ruby-implementations' ("pry". "pry"))
  (setq inf-ruby-default-implementation "pry")
  )
#+end_src

Avoid ridiculous Ruby indentation.

#+begin_src emacs-lisp
(setq ruby-deep-indent-paren nil)
#+end_src

Don't put the UTF-8 encoding comment at the top.

#+begin_src emacs-lisp
(setq ruby-insert-encoding-magic-comment nil)
#+end_src

Parentheses.

#+begin_src emacs-lisp
(require 'smartparens-ruby)
#+end_src

"Finds all the URLs in the buffer, highlights them, and turns them into clickable buttons."  Use =C-c RET= to follow a link.

TODO Make Org's =C-c C-o= more general and follow these links too.

#+begin_src emacs-lisp
(add-hook 'ruby-mode-hook #'goto-address-mode)
#+end_src

[[https://github.com/bbatsov/rubocop][Rubocop]] is a huge help when writing Ruby.

#+begin_src emacs-lisp
(use-package rubocop
  :diminish rubocop-mode
  :hook ruby-mode
  )
#+end_src

[[https://github.com/dgutov/robe][Robe]] for documentation lookup, etc.  (Requires the gems ~pry~ and ~pry-doc~.)

#+begin_src emacs-lisp
(use-package robe
   :diminish robe-mode
   :hook ruby-mode
   :config
   (eval-after-load 'company '(push 'company-robe company-backends))
   )
#+end_src

* Jekyll

My [[https://www.miskatonic.org/][personal web site]] is built on [[https://jekyllrb.com/][Jekyll]].  I use these three commands a lot:

+ ~C-c j n~: create new draft post
+ ~C-c j p~: publish the post
+ ~C-c j t~: update the timestamp in the post

I wrote this timestamp function myself.  It's the most complex Lisp thing I've ever written!

#+begin_src emacs-lisp
(defun jekyll-timestamp ()
  "Update existing date: timestamp on a Jekyll page or post."
  (interactive)
  (save-excursion (
		   goto-char 1)
		  (re-search-forward "^date:")
		  (let ((beg (point)))
		    (end-of-line)
		    (delete-region beg (point)))
		  (insert (concat " " (format-time-string "%Y-%m-%d %H:%M:%S %z"))))
  )
#+end_src

I got all this from a web site that no longer exists.  Such is the way of the internet.

#+begin_src emacs-lisp
(global-set-key (kbd "C-c j n") 'jekyll-draft-post)
(global-set-key (kbd "C-c j p") 'jekyll-publish-post)
(global-set-key (kbd "C-c j t") 'jekyll-timestamp)
(global-set-key (kbd "C-c j o") (lambda () (interactive) (find-file "~/web/")))

(defvar jekyll-directory "~/web/" "Path to Jekyll blog.")
(defvar jekyll-drafts-dir "_drafts/" "Relative path to drafts directory.")
(defvar jekyll-posts-dir "_posts/" "Relative path to posts directory.")
(defvar jekyll-post-ext ".md"  "File extension of Jekyll posts.")
(defvar jekyll-post-template "---\nlayout: post\ntitle: %s\ntags:\ndate: \n---\n"
  "Default template for Jekyll posts. %s will be replace by the post title.")

(defun jekyll-make-slug (s) "Turn string S into a slug."
       (replace-regexp-in-string " " "-"  (downcase (replace-regexp-in-string "[^A-Za-z0-9 ]" "" s))))

(defun jekyll-yaml-escape (s) "Escape string S for YAML."
       (if (or (string-match ":" s) (string-match "\"" s)) (concat "\"" (replace-regexp-in-string "\"" "\\\\\"" s) "\"") s))

(defun jekyll-draft-post (title) "Create a new Jekyll blog post with title TITLE."
       (interactive "sPost Title: ")
       (let ((draft-file (concat jekyll-directory jekyll-drafts-dir
				 (jekyll-make-slug title)
				 jekyll-post-ext)))
	 (if (file-exists-p draft-file)
             (find-file draft-file)
	   (find-file draft-file)
	   (insert (format jekyll-post-template (jekyll-yaml-escape title))))))

(defun jekyll-publish-post () "Move a draft post to the posts directory, and rename it to include the date."
       (interactive)
       (cond
	((not (equal
               (file-name-directory (buffer-file-name (current-buffer)))
               (expand-file-name (concat jekyll-directory jekyll-drafts-dir))))
	 (message "This is not a draft post.")
	 (insert (file-name-directory (buffer-file-name (current-buffer))) "\n"
		 (concat jekyll-directory jekyll-drafts-dir)))
	((buffer-modified-p)
	 (message "Can't publish post; buffer has modifications."))
	(t
	 (let ((filename
		(concat jekyll-directory jekyll-posts-dir
			(format-time-string "%Y-%m-%d-")
			(file-name-nondirectory
			 (buffer-file-name (current-buffer)))))
               (old-point (point)))
	   (rename-file (buffer-file-name (current-buffer))
			filename)
	   (kill-buffer nil)
	   (find-file filename)
	   (set-window-point (selected-window) old-point)))))
#+end_src

* LaTeX

Except: auto-completion in Auctex is turned on elsewhere.

Remember: when editing tables, use ~M-x align-current~.

Good reading:

+ http://tex.stackexchange.com/questions/50827/a-simpletons-guide-to-tex-workflow-with-emacs/50919#50919
+ http://tex.stackexchange.com/questions/52179/what-is-your-favorite-emacs-and-or-auctex-command-trick

#+begin_src emacs-lisp
(add-hook 'LaTeX-mode-hook #'outline-minor-mode)
#+end_src

[[https://www.gnu.org/software/auctex/][Auctex]] is amazingly powerful.

#+begin_src emacs-lisp
(use-package auctex
  :defer t
  :diminish auctex
  )
#+end_src

Always use visual-line-mode.

#+begin_src emacs-lisp
(add-hook 'LaTeX-mode-hook 'turn-on-visual-line-mode)
#+end_src

Turn on spell-checking.

#+begin_src emacs-lisp
(add-hook 'LaTeX-mode-hook 'flyspell-mode)
#+end_src

Use =pdflatex= to make PDFs.

#+begin_src emacs-lisp
;; (setq latex-run-command "pdflatex")
;; Use pdflatex to make PDFs
;; For some reason this value isn't respected and I had to set
;; it through Custom. Don't know why.
(setq TeX-PDF-mode t)
#+end_src

Bibliographies.

#+begin_src emacs-lisp
(setq biblatex-dialect "biblatex")
#+end_src

Automatically activate TeX-fold-mode.  ~C-c C-o C-b~ is necessary to hide everything (or see LaTeX | Show/Hide)

#+begin_src emacs-lisp
(add-hook 'TeX-mode-hook (lambda () (TeX-fold-mode 1)))
#+end_src

Use =wrap-region=.

#+begin_src emacs-lisp
(add-hook 'latex-mode-hook 'wrap-region-mode)
#+end_src

Indent lists by 2 (default is -2).

#+begin_src emacs-lisp
(setq LaTeX-item-indent 0)
#+end_src

Let me do some narrowing in LaTeX documents ... but narrow-or-widen-dwim (~C-x w~) doesn't focus on a section or subsection?!

#+begin_src emacs-lisp
(put 'LaTeX-narrow-to-environment 'disabled nil)
(put 'TeX-narrow-to-group 'disabled nil)
#+end_src

RefTeX.

#+begin_src emacs-lisp
(add-hook 'LaTeX-mode-hook 'turn-on-reftex)
(setq reftex-plug-into-AUCTeX t)
(eval-after-load "reftex" '(diminish 'reftex-mode))
(setq reftex-bibliography-commands '("bibliography" "nobibliography" "addbibresource"))
#+end_src

From [[http://tex.stackexchange.com/questions/50827/a-simpletons-guide-to-tex-workflow-with-emacs/50919#50919][Stack Overflow, as usual]].

#+begin_src emacs-lisp
(eval-after-load 'reftex-vars
  '(progn
     ;; (also some other reftex-related customizations)
     (setq reftex-cite-format
           '((?\C-m . "\\cite[]{%l}")
             (?f . "\\footcite[][]{%l}")
             (?t . "\\textcite[]{%l}")
             (?p . "\\parencite[]{%l}")
             (?o . "\\citepr[]{%l}")
             (?n . "\\nocite{%l}")))))
#+end_src

* Polymode (not in use)

[[https://github.com/vspinu/polymode/][Polymode]], for Markdown + R + Yaml etc.

I don't use this right now---it would mess up headers in YAML files---but maybe I'll come back to it.  For now, it's not tangled.

#+begin_src emacs-mode :tangle no
(use-package polymode)

;; Polymode is nice everywhere, except I do not want it in Org.

(use-package poly-markdown
  :config
  (add-to-list 'auto-mode-alist '("\\.md$" . poly-markdown-mode))
  (setq markdown-hide-urls t)
  (setq markdown-hide-markup t)
  (setq markdown-url-compose-char "‚ãØ")
  (setq markdown-header-scaling t)
  (add-hook 'markdown-mode-hook 'turn-on-visual-line-mode)
  )

(use-package poly-R
  :config
  (add-to-list 'auto-mode-alist '("\\.Rmd$" . poly-markdown+r-mode))
  )

(use-package poly-noweb)
#+end_src

* Esonify (not in use)

[[https://github.com/oflatt/esonify][Esonify]] is "an Emacs extension that sonifies your code."  ~M-x esonify-mode~ to toggle on/off.

#+begin_src emacs-lisp :tangle no
(use-package esonify)
#+end_src
